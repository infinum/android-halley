buildscript {
    apply from: "config.gradle"
    apply from: "maven.gradle"
}

plugins {
    alias libs.plugins.kotlin.jvm apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.android.application apply false
    alias libs.plugins.kotlinx.serialization apply false
    alias libs.plugins.detekt apply false
    alias libs.plugins.ktlint apply false
    alias libs.plugins.cpd apply false
    alias libs.plugins.dokka apply false
}

subprojects {
    apply from: "$rootDir/cpd.gradle"
    apply from: "$rootDir/detekt.gradle"
    apply from: "$rootDir/ktlint.gradle"
    apply from: "$rootDir/dokka.gradle"
}

apply from: "deploy.gradle"

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(dependsOn: [
        ':halley-core:detekt',
        ':halley-retrofit:detekt',
        ':halley-ktor:detekt'
]) {
    group = "Verification"
    description = "Run Detekt on all modules"
}

task ktlintAll(dependsOn: [
        ':halley-core:ktlintCheck',
        ':halley-retrofit:ktlintCheck',
        ':halley-ktor:ktlintCheck'
]) {
    group = "Verification"
    description = "Run ktLint on all modules"
}

task cpdAll(dependsOn: [
        ':halley-core:cpdCheck',
        ':halley-retrofit:cpdCheck',
        ':halley-ktor:cpdCheck'
]) {
    group = "Verification"
    description = "Run CPD on all modules"
}

task runStaticChecks(dependsOn: [
        ':detektAll',
        ':ktlintAll',
        ':cpdAll'
]) {
    group = "Verification"
    description = "Run static checks on all modules"
}