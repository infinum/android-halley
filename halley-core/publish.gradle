apply plugin: "maven-publish"
apply plugin: "signing"

task sourcesJar(type: Jar, dependsOn: assemble) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name sonatype.name()
                url sonatype.url()
                credentials {
                    username sonatype.username()
                    password sonatype.password()
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.java

                groupId = releaseConfig.group
                artifactId = "halley-core"
                version = releaseConfig.version

                artifact javadocsJar
                artifact sourcesJar

                pom {
                    name = "Halley JSON HAL Core"
                    description = "Core library for JSON HAL Android implementation."
                    url = "https://github.com/infinum/android-halley"
                    licenses {
                        license {
                            name = "Apache License 2.0"
                            url = "https://github.com/infinum/android-halley/blob/master/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "mariciv"
                            name = "Ivan Marić"
                            email = "ivan.maric@infinum.com"
                        }
                        developer {
                            id = "knobtviker"
                            name = "Bojan Komljenović"
                            email = "bojan.komljenovic@infinum.com"
                        }
                    }
                    scm {
                        connection = "https://github.com/infinum/android-halley.git"
                        developerConnection = "https://github.com/infinum/android-halley.git"
                        url = "https://github.com/infinum/android-halley"
                    }
                }
                signing {
                    sign publishing.publications.release
                }
            }
        }
    }
}
